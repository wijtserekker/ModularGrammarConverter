module main ;
using ctl ;
using ltl ;
using mucalc ;

expr ::= ctl.expr
     | ltl.expr
     | mucalc.expr
     ;




module mucalc ;
using pred ;
using bool ;

expr <= bool.expr ;
expr ::= MU pred.VAR DOT expr
     | NU pred.VAR DOT expr
     | pred.LT pred.VAR pred.GT expr
     | EDGE_ALL_LEFT pred.VAR EDGE_ALL_RIGHT expr
     | pred.NEXT expr
     | pred.EXIST expr
     | pred.ALL expr
     | pred.comp_expr
     ;

MU ::= 'mu' ;
NU ::= 'nu' ;
EDGE_ALL_LEFT    ::= '[' ;
EDGE_ALL_RIGHT   ::= ']' ;
DOT   ::= '.' ;




module ctl ;
using bool ;
using pred ;

expr <= bool.expr ;
expr <= bool.expr_extra ;
expr ::= pred.EXIST expr
     | pred.ALL expr
     | pred.GLOBALLY expr
     | pred.FUTURE expr
     | pred.NEXT expr
     | expr pred.UNTIL expr
     | pred.comp_expr
     ;


module ltl ;
using pred ;
using bool ;

expr <= bool.expr ;
expr <= bool.expr_extra ;
expr ::= pred.GLOBALLY expr
     | pred.FUTURE expr
     | pred.NEXT expr
     | expr pred.UNTIL expr
     | expr WEAK_UNTIL expr
     | expr RELEASE expr
     | pred.comp_expr
     ;

WEAK_UNTIL ::= 'W' ;
RELEASE    ::= 'R' ;


module pred ;
using bool ;

comp_expr ::= calc_expr EQ calc_expr
            | calc_expr NEQ calc_expr
            | calc_expr LT calc_expr
            | calc_expr LEQ calc_expr
            | calc_expr GT calc_expr
            | calc_expr GEQ calc_expr
            | calc_expr EN calc_expr
            ;

calc_expr ::= VAR
            | NUMBER
            | STRING
            | CHUNK
            | bool.LPAR calc_expr bool.RPAR
            | calc_expr MULT calc_expr
            | calc_expr DIV calc_expr
            | calc_expr REM calc_expr
            | calc_expr ADD calc_expr
            | calc_expr SUB calc_expr
            ;

STRING    ::= '"' (~[\\"] | '\\'$)* '"' ;
CHUNK     ::= '#' ([0-9a-fA-F] [0-9a-fA-F])* '#' ;
NUMBER    ::= [0-9]+ ;
VAR       ::= '_'* ([a-zA-Z] | '\\'$) ([a-zA-Z0-9_'] | '\\'$)* ;

MULT  ::= '*' ;
DIV   ::= '/' ;
REM   ::= '%' ;
ADD   ::= '+' ;
SUB   ::= '-' ;
LT    ::= '<' ;
LEQ   ::= '<=' ;
GT    ::= '>' ;
GEQ   ::= '>=' ;
EQ    ::= '==' ;
NEQ   ::= '!=' ;
EN    ::= '??' ;

GLOBALLY   ::= '[]' ;
FUTURE     ::= '<>' ;
NEXT ::= 'X' ;
EXIST ::= 'E' ;
ALL ::= 'A' ;
UNTIL ::= 'U' ;



module bool;

expr ::= expr AND expr
     | expr OR expr
     | NOT expr
     | TRUE
     | FALSE
     | MAYBE
     | LPAR expr RPAR
     ;

expr_extra ::= expr_extra EQUIV expr_extra
           | expr_extra IMPLY expr_extra
           ;

LPAR  ::= '(' ;
RPAR  ::= ')' ;
TRUE  ::= 'true' ;
FALSE ::= 'false' ;
MAYBE ::= 'maybe' ;
NOT   ::= '!' ;
AND   ::= '&&' ;
OR    ::= '||' ;
EQUIV ::= '<->' ;
IMPLY ::= '->' ;